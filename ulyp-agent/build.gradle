plugins {
    id 'java'
    id 'io.freefair.lombok' version '5.3.0'
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

dependencies {
    implementation project(':ulyp-common')
    compileOnly project(':ulyp-agent-core')
    runtimeOnly files('../ulyp-agent-core/build/libs/ulyp-agent-core.jarrr')

    implementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.14.13'

    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.2'
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
    testImplementation project(':ulyp-agent-core')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

sourceSets {
    main.java.srcDirs = ['src/main/java', '../src/main/java']
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version2.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['build_time'] = String.valueOf(System.currentTimeMillis())
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

compileJava {
    options.debug = true
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

shadowJar {
    dependsOn(':ulyp-agent-core:shadowJar')
    dependsOn(':ulyp-agent-core:copyShadowJar')

    relocate 'com.ulyp', 'com.ulyp2'
    relocate 'org.agrona', 'shadowed2.org.agrona'
    relocate 'org.slf4j', 'com.ulyp2.slf4j'
    relocate 'io', 'shadowed2.io'
    relocate 'com.google', 'shadowed2.com.google'
    relocate 'javax.annotation', 'shadowed2.javax.annotation'
    relocate 'net.bytebuddy', 'shadowed2.net.bytebuddy'

    archiveName = "${rootProject.name}-agent-${rootProject.version}.jar"

    mergeServiceFiles()

    manifest {
        attributes(
                'Main-Class': 'com.ulyp.agent.Main',
                'Premain-Class': 'com.ulyp2.agent.AgentBootstrap',
                'Agent-Class': 'com.ulyp2.agent.AgentBootstrap',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Can-Set-Native-Method-Prefix': 'true',
                'Implementation-Title': "UlypAgent",
                'Implementation-Version': rootProject.version,
                'Built-By': 0xaa4eb,
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }
}

assemble {
    dependsOn('shadowJar')
}

test {
    dependsOn('shadowJar')
}
