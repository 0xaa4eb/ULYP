plugins {
    id 'java'
    id 'io.freefair.lombok' version '5.3.0'
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':ulyp-common')
    compileOnly project(':ulyp-agent-classes')
    runtimeOnly files('../ulyp-agent-classes/build/libs/ulyp-agent-classes.jarr')

    implementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.12.20'

    compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
    compileOnly group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.32'
    compileOnly group: 'org.jetbrains', name: 'annotations', version: jetbrains_annotations_version
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.24'

    testImplementation project(':ulyp-agent-classes')
    testImplementation group: 'junit', name: 'junit', version: '4.11'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
}

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDirs = ['src/main/java', '../src/main/java']
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['build_time'] = String.valueOf(System.currentTimeMillis())
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

compileJava {
    options.debug = true
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

shadowJar {
    dependsOn(':ulyp-agent-classes:shadowJar')
    dependsOn(':ulyp-agent-classes:copyShadowJar')

    relocate 'org.agrona', 'shadowed.org.agrona'
    relocate 'org.slf4j', 'ulyp.org.slf4j'
    relocate 'it.unimi', 'shadowed.it.unimi'
    relocate 'io', 'shadowed.io'
    relocate 'com.google', 'shadowed.com.google'
    relocate 'javax.annotation', 'shadowed.javax.annotation'
    relocate 'net.bytebuddy', 'shadowed.net.bytebuddy'

    archiveName = "${rootProject.name}-agent-${rootProject.version}.jar"

    mergeServiceFiles()

    manifest {
        attributes(
                'Main-Class': 'com.ulyp.agent.Main',
                'Premain-Class': 'com.ulyp.agent.AgentBootstrap',
                'Agent-Class': 'com.ulyp.agent.AgentBootstrap',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Can-Set-Native-Method-Prefix': 'true',
                'Implementation-Title': "UlypAgent",
                'Implementation-Version': rootProject.version,
                'Built-By': 0xaa4eb,
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }
}

assemble {
    dependsOn('shadowJar')
}

test {
    dependsOn('shadowJar')
}
