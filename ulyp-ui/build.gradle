buildscript {
    repositories {

        mavenCentral()
        jcenter()

        flatDir {
            dirs 'ulyp-ui/fernflower.jar'
        }
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.lombok' version '1.6.21'
    id 'io.freefair.lombok' version '5.3.0'
    id 'org.openjfx.javafxplugin' version '0.0.10' apply(false)
}

def javaVersion = JavaVersion.current()

if (javaVersion >= JavaVersion.VERSION_1_7 && javaVersion < JavaVersion.VERSION_1_9) {
    apply from: "build-jdk7.gradle"
} else if (javaVersion >= JavaVersion.VERSION_11) {
    apply from: "build-jdk11.gradle"
} else {
    throw new GradleException("JITWatch is unsupported on Java $javaVersion")
}

def protobufVersion = '3.11.0'
def protocVersion = protobufVersion
def grpcVersion = '1.27.0'

dependencies {
    implementation project(':ulyp-common')
    implementation project(':ulyp-storage')
    implementation project(':ulyp-agent-api')
    implementation files('libs/fernflower.jar')

    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation group: 'com.google.guava', name: 'guava', version: guava_version
    implementation group: 'io.vavr', name: 'vavr', version: vavr_version

    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"

    implementation group: 'org.springframework', name: 'spring-beans', version: spring_version
    implementation group: 'org.springframework', name: 'spring-context', version: spring_version
    implementation group: 'org.springframework', name: 'spring-core', version: spring_version

    implementation group: 'org.rocksdb', name: 'rocksdbjni', version: rocksdb_version
    implementation group: 'it.unimi.dsi', name: 'fastutil', version: fastutil_version

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombok_version
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombok_version

    implementation group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '3.1.1'
    implementation group: 'com.github.javaparser', name: 'javaparser-core', version: '3.16.1'
    implementation group: 'org.agrona', name: 'agrona', version: agrona_version
    implementation group: 'org.kordamp.bootstrapfx', name: 'bootstrapfx-core', version: '0.2.3'

    implementation group: 'org.apache.logging.log4j', name: 'log4j', version: '2.13.1'
    implementation group: 'org.jetbrains', name: 'annotations', version: '18.0.0'

    testImplementation group: 'junit', name: 'junit', version: junit_version
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: bytebuddy_version
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: hamcrest_version
}

mainClassName = 'com.ulyp.ui.UIStarter'

sourceSets {
    main.java.srcDirs = ['src/main/java', '../src/main/java', 'src/main/kotlin', '../src/main/kotlin']
}

run {
    workingDir = rootDir
}

task fatJar(type: Jar) {
    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Implementation-Title': "Ulyp UI",
                'Implementation-Version': rootProject.version,
                'Built-By': '0xaa4eb',
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }

    group = 'build'
    archiveName = "ulyp-ui-${rootProject.version}.jar"
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}