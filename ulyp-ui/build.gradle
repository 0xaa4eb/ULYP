buildscript {
    repositories {
        flatDir {
            dirs 'ulyp-ui/fernflower.jar'
        }
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.lombok' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.21'
    id 'io.freefair.lombok' version '5.3.0'
    id 'org.openjfx.javafxplugin' version '0.0.14' apply(false)
}

def javaVersion = JavaVersion.current()

if (javaVersion >= JavaVersion.VERSION_1_7 && javaVersion < JavaVersion.VERSION_1_9) {
    apply from: "build-jdk7.gradle"
} else if (javaVersion >= JavaVersion.VERSION_11) {
    apply from: "build-jdk11.gradle"
} else {
    throw new GradleException("JITWatch is unsupported on Java $javaVersion")
}

dependencies {
    implementation project(':ulyp-common')
    implementation project(':ulyp-storage')
    implementation files('libs/fernflower.jar')

    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-json', version: '1.4.1'

    implementation group: 'org.springframework', name: 'spring-beans', version: '5.2.8.RELEASE'
    implementation group: 'org.springframework', name: 'spring-context', version: '5.2.8.RELEASE'
    implementation group: 'org.springframework', name: 'spring-core', version: '5.2.8.RELEASE'

    implementation group: 'org.rocksdb', name: 'rocksdbjni', version: '7.2.2'
    implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.12'
    implementation group: 'org.agrona', name: 'agrona', version: '1.4.0'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.24'

    implementation group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '3.4.0'
    implementation group: 'com.github.javaparser', name: 'javaparser-core', version: '3.16.1'

    implementation group: 'org.apache.logging.log4j', name: 'log4j', version: '2.20.0'
    implementation group: 'org.jetbrains', name: 'annotations', version: '18.0.0'

    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.14.4'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
}

mainClassName = 'com.ulyp.ui.UIStarter'

sourceSets {
    main.java.srcDirs = ['src/main/java', '../src/main/java', 'src/main/kotlin', '../src/main/kotlin']
}

run {
    workingDir = rootDir
}

def platform = project.findProperty("platform")
def jarName
if (platform != null) {
    jarName = "ulyp-${rootProject.version}-${platform}.jar"
} else {
    jarName = "ulyp-${rootProject.version}.jar"
}

task fatJar(type: Jar) {
    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Implementation-Title': "Ulyp UI",
                'Implementation-Version': rootProject.version,
                'Built-By': '0xaa4eb',
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }

    group = 'build'
    archiveFileName = jarName
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}