buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'io.freefair.lombok' version '5.3.0'
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

repositories {
    mavenCentral()
}

def protobufVersion = '3.11.0'
def protocVersion = protobufVersion
def grpcVersion = '1.27.0'

dependencies {
    compile project(':ulyp-common')
    compile project(':ulyp-agent-api')
    compile project(':ulyp-storage')

    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"

    compile group: 'it.unimi.dsi', name: 'fastutil', version: '8.2.1'
    compile group: 'org.agrona', name: 'agrona', version: '0.9.22'
    compile group: 'org.jetbrains', name: 'annotations', version: '18.0.0'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombok_version
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombok_version

    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

    testCompile group: 'net.bytebuddy', name: 'byte-buddy', version: bytebuddy_version
    testCompile group: 'junit', name: 'junit', version: junit_version
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: hamcrest_version
}

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDirs = ['src/main/java', '../src/main/java']
}

compileJava {
    options.debug = true
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

shadowJar {
    relocate 'org.agrona', 'shadowed.org.agrona'
    relocate 'org.slf4j', 'ulyp.org.slf4j'
    relocate 'it.unimi', 'shadowed.it.unimi'
    relocate 'io', 'shadowed.io'
    relocate 'com.google', 'shadowed.com.google'
    relocate 'javax.annotation', 'shadowed.javax.annotation'
    relocate 'net.bytebuddy', 'shadowed.net.bytebuddy'

    archiveName = "${rootProject.name}-agent-classes-${rootProject.version}.jar"

    mergeServiceFiles()

    manifest {
        attributes(
                'Implementation-Title': "UlypAgent",
                'Implementation-Version': rootProject.version,
                'Built-By': 0xaa4eb,
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }
}

tasks.register('copyShadowJar', Copy) {
    from layout.buildDirectory.file('libs/ulyp-agent-classes-' + rootProject.version + '.jar')
    into layout.buildDirectory.file('libs')
    rename { String fileName ->
        fileName = fileName.replace("-$project.version", "")
        fileName + "r"
    }
    dependsOn('shadowJar')
}

assemble {
    dependsOn('shadowJar', 'copyShadowJar')
}
